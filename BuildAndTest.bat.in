@ECHO off 

SET BUILD_TYPE=Release

REM Parse arguments
SET COMMAND=%0
SET TEST_MODE=%1
SHIFT

:loop
IF NOT "%1"=="" (
    IF "%1"=="--test" (
        SET INDIVIDUAL_TEST=%2
        SHIFT
    )
    IF "%1"=="--track" (
        SET TEST_TRACK=--track %2
        SHIFT
    )
    SHIFT
    GOTO :loop
)

REM Skip build step if continuous or individual mode
REM Skip clean build step if not nightly mode
if "%TEST_MODE%" == "-C" goto cleansuccess
if "%TEST_MODE%" == "-I" goto cleansuccess
if "%TEST_MODE%" == "" goto cleansuccess
if "%TEST_MODE%" == "-E" goto cleansuccess

ECHO Clean...
"${CMAKE_MAKE_PROGRAM}" ALL_BUILD.vcxproj /p:Configuration=Release /target:clean
IF ERRORLEVEL 1 GOTO buildfail

:cleansuccess

rem ---------------------------------------
if "%TEST_MODE%" == "" goto experimental
if "%TEST_MODE%" == "-E" goto experimental
if "%TEST_MODE%" == "-N" goto nightly
if "%TEST_MODE%" == "-C" goto continuous
if "%TEST_MODE%" == "-I" goto individual

:experimental
"${CMAKE_CTEST_COMMAND}" -C %BUILD_TYPE% -D Experimental --output-on-failure %TEST_TRACK%
goto success

:nightly
@REM Clean before the nightly build to enforce all build warnings appear on all nightly dashboard submissions
"${CMAKE_CTEST_COMMAND}" -C %BUILD_TYPE% -D Nightly %TEST_TRACK%
goto success

:continuous
"${CMAKE_CTEST_COMMAND}" -C %BUILD_TYPE% -D Continuous %TEST_TRACK%
@REM Wait for some time before continue to allow checking the results of the executions
timeout /t 15
goto success

:individual 
@REM Run individual tests with regexp search 
@REM Display the list of tests
"${CMAKE_CTEST_COMMAND}" -C %BUILD_TYPE% -R "%INDIVIDUAL_TEST%" -N %TEST_TRACK%
@REM Run selected tests
"${CMAKE_CTEST_COMMAND}" -C %BUILD_TYPE% -R "%INDIVIDUAL_TEST%" -V %TEST_TRACK%
goto success

:success
exit /b 0

:buildfail
ECHO Failed to build PlusLib
exit /b 1
